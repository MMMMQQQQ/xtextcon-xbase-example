grammar org.xtextcon.xbase.smarthome.Rules with org.eclipse.xtext.xbase.Xbase

generate rules "http://www.xtextcon.org/xbase/smarthome/Rules"
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

Model:
	declarations+=Declaration*;

Declaration:
	Device | Rule
;

Rule:
	'When'
	// simplified, in practice we'd like to allow expressions here, too
	(when=[State|QualifiedName] | time=TimeLiteral) 
	'then' then=XExpression 
;

Device:
	name=ID 'can' 'be' states+=State (',' states+=State)*
;

State:
	name=ID
;

XLiteral returns xbase::XExpression:
	TimeLiteral |
	XCollectionLiteral |
	XClosure |
	XBooleanLiteral |
	XNumberLiteral |
	XNullLiteral |
	XStringLiteral |
	XTypeLiteral
;

TimeLiteral:
	// the predicates convince Antlr that this is really a time literal
	=>(hour=INT ':') min=INT (=>':' sec=INT)?
;